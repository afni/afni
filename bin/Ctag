#!/bin/tcsh

# list of allowable NAME values -- edit here if you want more

set llist = ( AFNI SUMA RICKR )

if( $#argv < 1 || "$1" == "-help" )then
   echo " Usage: Ctag NAME"
   echo "  + Write a version string tag to all CVS files in the cwd and below."
   echo "  + The tag is the output of the command 'date +NAME_%Y_%m_%d_%H:%M'"
   echo "    where the NAME is input on the command line."
   echo "  + You should check out and in (i.e., 'Cget ; Cput .') before using Ctag."
   echo "  + Also, will create a file named NAME_label.h in the cwd."
   echo "  + The legal values for NAME are: ${llist} ."
   exit 0
endif

# scan to make sure the supplied NAME (in variable nnn) is in the list

set nnn = ${1}

set nll = $#llist
set iii = 1
while( $iii <= $#llist )
   if( $llist[$iii] == $nnn ) break
   @ iii++
end
if( $iii > $#llist )then
  echo "NAME = $nnn is not in the legal list: ${llist} ."
  exit 1
endif

# Create the tag label for CVS

set ttt = `date +${nnn}_%Y_%m_%d_%H%M`
echo "Tag set to ${ttt}"

# Create the C header file with the tag label

set fff = ${nnn}_label.h

if( -f ${fff} )then
  set do_Cadd = 0
  echo "Rewriting file ${nnn}_label.h"
else
  set do_Cadd = 1
  echo "Creating file ${nnn}_label.h"
endif

echo "#undef  ${nnn}_VERSION_LABEL"             >  ${nnn}_label.h
echo "#define ${nnn}_VERSION_LABEL " \"${ttt}\" >> ${nnn}_label.h

# And now do the CVS stuff, ending with the setting of the tag

echo "Sending file and tag to CVS"

if( do_Cadd == 1 ) cvs add ${fff}
cvs commit -m "Ctag" ${fff}
cvs tag -R ${ttt}
