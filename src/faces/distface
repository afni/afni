#!/bin/tcsh

# Shell script to put faces into files that
# are accessible by anonymous FTP -- RWCox.

# Usage: distface [-html]
# -- to be run inside the AFNI/src/faces directory

# loop over relevan directories in the FTP hierarchy

if( $?argv && $#argv > 0 ) then
  if( "$argv[1]" == "-html" ) goto html
endif

foreach ftpdir ( /FTP/AFNI/bin/* /FTP/AFNI/src/faces ~/abin )

   if( -d $ftpdir ) then
     echo "===== Starting to install faces into $ftpdir"
     foreach fname ( face_*.jpg )
       if( -f $ftpdir/$fname ) then
          cmp -s $ftpdir/$fname $fname
          set mmm = $status
          if( $mmm > 1 ) set mmm = 0
       else
          set mmm = 1
       endif
       if( $mmm ) then
         echo "/bin/cp -pf $fname $ftpdir"
               /bin/cp -pf $fname $ftpdir
       endif
       chmod -R ugo+r $ftpdir
       chmod -R go-w  $ftpdir
     end
   else
       echo "+++++ $ftpdir isn't a directory"
   endif

end

html:

echo "===== Making afni_faces.html"

echo  '<html><head><title>AFNI User Images</title></head>'                        > afni_faces.html
echo  '<body><center><h1><i>AFNI</i> User Images</h1>'                           >> afni_faces.html
echo  '<h2>Contributed voluntarily by each user</h2>'                            >> afni_faces.html
echo  'To contribute your personal image, send a JPEG (.jpg) file'               >> afni_faces.html
echo  'no larger than 128x128 to'                                                >> afni_faces.html
echo  '<a href="mailto:rwcox@nih.gov">rwcox@nih.gov</a>.<br />'                  >> afni_faces.html
echo  'These images are also shown randomly in the <i>AFNI</i> splash screen.'   >> afni_faces.html
echo  '<hr />'                                                                   >> afni_faces.html
echo  '<table cellpadding=2 cellspacing=0 border=0><tr><td bgcolor="#eecc00">'   >> afni_faces.html
echo  '<table cellpadding=2 cellspacing=0 border=0><tr><td bgcolor="#cc9900">'   >> afni_faces.html
echo  '<table cellpadding=2 cellspacing=0 border=0><tr><td bgcolor="#aa6600">'   >> afni_faces.html
echo  '<table cellpadding=2 cellspacing=0 border=0><tr><td bgcolor="#883300">'   >> afni_faces.html
echo  '<table cellpadding=2 cellspacing=0 border=0><tr><td bgcolor="#ffdddd">'   >> afni_faces.html
echo  '<table cellpadding=3 cellspacing=1 border=1>'                             >> afni_faces.html
echo  '<tr>'                                                                     >> afni_faces.html

set nn = 0

foreach face ( face_*.jpg )

  if( $nn == 0 ) then
    echo '</tr>'                                          >>   afni_faces.html
    echo '<tr>'                                           >>   afni_faces.html
  endif

  if( "$face" == "face_rwcox2.jpg" ) continue ;

  echo '<td align="center"> <img src="'${face}'" /></td>' >>   afni_faces.html

  @ nn = ($nn + 1) % 5
end

echo '</tr></table>'                                      >>   afni_faces.html
echo '</td></tr></table>'                                 >>   afni_faces.html
echo '</td></tr></table>'                                 >>   afni_faces.html
echo '</td></tr></table>'                                 >>   afni_faces.html
echo '</td></tr></table>'                                 >>   afni_faces.html
echo '</td></tr></table>'                                 >>   afni_faces.html
echo '<hr /><b>This page generated on'                    >>   afni_faces.html
date                                                      >>   afni_faces.html
echo '</b><p>'                                            >>   afni_faces.html
echo '<a href="http://afni.nimh.nih.gov/afni">'           >>   afni_faces.html
echo '<i>AFNI</i> main page</a>'                          >>   afni_faces.html
echo '</center></body></html>'                            >>   afni_faces.html

/bin/cp -f afni_faces.html face_*.jpg ~/afni/faces/
