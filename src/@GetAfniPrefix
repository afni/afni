#!/bin/tcsh -f
if ("$1" == '' || "$1" == '-h' || "$1" == '-help') then
	goto USAGE
endif

set noglob
#remove path
set Name = $1:t
#remove extension .HEAD or .BRIK
set ext = $Name:e
set fext = ""
if ("$ext" == "gz") then
   #remove .gz first
   set Name = $Name:r
   set ext = $Name:e
   set fext = ".gz"
else if ("$ext" == "bz2") then
   set Name = $Name:r
   set ext = $Name:e
   set fext = ".bz2"
else if ("$ext" == "Z") then
   set Name = $Name:r
   set ext = $Name:e
   set fext = ".Z"
else if ("$ext" == ".BRIK" || "$ext" == ".HEAD") then
   set Name = $Name:r
   set fext = ""
endif
if ("$ext" == "nii") then
   set Name = $Name.$ext"$fext"
   goto ECHO
endif
    
#Remove extension only if it is .HEAD or .BRIK
if ("$ext" == "HEAD" || "$ext" == "BRIK") then
   set Name = $Name:r
endif
#remove +orig or whatever it is
set Name = `echo $Name | sed 's/+orig.//'`
set Name = `echo $Name | sed 's/+acpc.//'`
set Name = `echo $Name | sed 's/+tlrc.//'`
set Name = `echo $Name | sed 's/+orig//'`
set Name = `echo $Name | sed 's/+acpc//'`
set Name = `echo $Name | sed 's/+tlrc//'`
goto ECHO

ECHO:
echo $Name
unset noglob

goto ENDALL

USAGE:
	echo "Usage: `basename $0` <Name> ....."
	echo "example: `basename $0` /Data/stuff/Hello+orig.HEAD"
	echo "returns the afni prefix of name (Hello)" 
	echo "Wildcards are treated as regular characters:"
	echo "example: `basename $0` 'AAzst1r*+orig'"
	echo "returns : AAzst1r*"
	echo ""
   echo "Ziad Saad (ziad@nih.gov)"
   echo "  LBC/NIMH/ National Institutes of Health, Bethesda Maryland"
   echo ""
	goto ENDALL
ENDALL:
