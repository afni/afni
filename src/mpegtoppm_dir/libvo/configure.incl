AC_SUBST([LIBVO_CFLAGS])
AC_SUBST([LIBVO_LIBS])

dnl check for X11
AC_PATH_XTRA
if test x"$no_x" != x"yes"; then
    dnl check for Xshm
    AC_CHECK_LIB([Xext],[XShmCreateImage],
	[AC_DEFINE([LIBVO_X11],,[libvo X11 support])
	LIBVO_CFLAGS="$LIBVO_CFLAGS $X_CFLAGS"
	LIBVO_LIBS="$LIBVO_LIBS $X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS -lXext"
	dnl check for Xv
	AC_CHECK_LIB([Xv],[XvShmCreateImage],
	    [AC_DEFINE([LIBVO_XV],,[libvo Xv support])
	    LIBVO_LIBS="$LIBVO_LIBS -lXv"],,
	    [$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS -lXext])],,
	[$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS])
fi

dnl check for DirectX
AC_ARG_ENABLE([directx],
    [  --enable-directx=DIR    use Win32 DirectX headers in DIR])
case $enable_directx in
    ""|yes)	AC_CHECK_HEADERS([ddraw.h],
		    [AC_DEFINE([LIBVO_DX],,[libvo DirectX support])
		    LIBVO_LIBS="$LIBVO_LIBS -lgdi32"]);;
    no)		;;
    *)		if test -f "$enable_directx/ddraw.h"; then
		    AC_DEFINE([LIBVO_DX],,[libvo DirectX support])
		    LIBVO_CFLAGS="$LIBVO_CFLAGS -I$enable_directx"
		    LIBVO_LIBS="$LIBVO_LIBS -lgdi32"
		else
		    AC_MSG_ERROR([Cannot find $enable_directx/ddraw.h])
		fi;;
esac

dnl check for mlib
AC_ARG_ENABLE([mlib],
    [  --disable-mlib          make a version not using mediaLib])
if test x"$enable_mlib" != x"no"; then
    cflags_save="$CFLAGS"
    CFLAGS="$CFLAGS -L/opt/SUNWmlib/lib -R/opt/SUNWmlib/lib"
    AC_CHECK_LIB([mlib],[mlib_VideoColorYUV2RGB420],
	[AC_DEFINE([LIBVO_MLIB],,[libvo mediaLib support])
	LIBVO_CFLAGS="$LIBVO_CFLAGS -I/opt/SUNWmlib/include"
	LIBVO_LIBS="$LIBVO_LIBS -L/opt/SUNWmlib/lib -R/opt/SUNWmlib/lib -lmlib"])
    CFLAGS="$cflags_save"
fi

dnl check for SDL
AC_ARG_ENABLE([sdl],[  --disable-sdl           make a version not using SDL])
if test x"$enable_sdl" != x"no"; then
    AC_CHECK_PROG([SDLCONFIG],[sdl-config],[yes])
    if test x"$SDLCONFIG" = x"yes"; then
	AC_DEFINE([LIBVO_SDL],,[libvo SDL support])
	LIBVO_CFLAGS="$LIBVO_CFLAGS `sdl-config --cflags`"
	LIBVO_LIBS="$LIBVO_LIBS `sdl-config --libs`"
    fi
fi
