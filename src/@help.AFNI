#!/bin/tcsh -f

PARSE:

   set Narg = $#
   set srch = ()
   set viewer = 'less'
   set url = https://afni.nimh.nih.gov/pub/dist/doc/program_help/all-of-them.html
   set urlclasspdf = https://afni.nimh.nih.gov/pub/dist/edu/latest
   #You can use something like: 
   #wget -r -l2 --no-parent -nd -A ".pdf" \ 
   #         https://afni.nimh.nih.gov/pub/dist/edu/latest/
   #to download all pdfs, say. Think about it....
   set cnt = 1
   while ($cnt <= $Narg)
		set donext = 1;
      if ($donext && "$argv[$cnt]" == "-echo") then
         set echo
         set donext = 0;		
      endif
      if ($donext && ("$argv[$cnt]" == "-h" || "$argv[$cnt]" == "-help")) then
         goto HELP
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-match") then
         @ cnt ++
         set srch = ("$srch" "$argv[$cnt]")
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-lynx") then
         set viewer = 'lynx'
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-less") then
         set viewer = 'less'
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-vi") then
         set viewer = 'vi'
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-nedit") then
         set viewer = 'nedit'
         set donext = 0;		
      endif
      if ($donext && "$argv[$cnt]" == "-noview") then
         set viewer = ''
         set donext = 0;		
      endif
      if ($donext == 1) then
         echo "Error: Option or parameter '$argv[$cnt]' not understood"
         goto END
      endif
      
		@ cnt ++
	end
   
   

cd /tmp

( find ./ -name all-of-them.html -mtime -1 > ttt ) > & /dev/null
set ll = `cat ttt`
if ( "$ll" == '' ) then
   echo "Retrieving $url"
   curl -o all-of-them.html $url
else
   echo "Reusing /tmp/all-of-them.html "      
endif

if ( ! -f all-of-them.html ) then
   echo "Failed to get all-of-them.html"
   goto BEND
endif

if ($#srch || ($viewer != '' && $viewer != 'lynx')) then
   lynx -dump all-of-them.html > all-of-them.txt
endif

if ($#srch) then
   set noglob
   set slist = ($srch) 
   echo "Matching lines:"
   switch ($#slist) 
      case 1: 
         \grep -sni $slist[1] all-of-them.txt
         breaksw
      case 2: 
         \grep -sni $slist[1] all-of-them.txt | \
               \grep -i $slist[2]
         breaksw
      case 3: 
         \grep -sni $slist[1] all-of-them.txt | \
               \grep -i $slist[2] | \grep -i $slist[3]
         breaksw
      case 4: 
         \grep -sni $slist[1] all-of-them.txt | \
               \grep -i $slist[2] | \grep -i $slist[3] | \
               \grep -i $slist[4] 
         breaksw
      case 5: 
         \grep -sni $slist[1] all-of-them.txt | \
               \grep -i $slist[2] | \grep -i $slist[3] | \
               \grep -i $slist[4] | \grep -i $slist[5]
         breaksw
      default:
         echo "Too many search strings"
         goto BEND
   endsw
endif

if ($viewer == 'lynx') then 
   xterm -T "$viewer-@help.AFNI.output" -e $viewer /tmp/all-of-them.html &
else if ($viewer == 'vi' || $viewer == 'less') then
   xterm -T "$viewer-@help.AFNI.output" -e $viewer all-of-them.txt &
else if ($viewer == 'nedit') then
   nedit /tmp/all-of-them.txt &
endif

cd -
goto END

HELP:
   echo ""
   echo "`basename $0` [OPTIONS]"
   echo "   A script to retrieve and search AFNI's help page for all programs"
   echo ""
   echo " Examples:"
   echo "   @help.AFNI "
   echo ""
   echo "   @help.AFNI -match love"
   echo ""
   echo " Options:"
   echo "   -match 'word1 [word2 word3]' : Looks for occurence of each word in "
   echo "                                  the list in the help file."
   echo "                                  For a match with multiple words, all"
   echo "                                  the words must be on the same line of"
   echo "                                  text in the help file."
   echo "   -lynx :  Set viewer to lynx"
   echo "   -vi   :  Set viewer to vi"
   echo "   -less :  Set viewer to less (default)"
   echo "   -nedit:  Set viewer to nedit"
   echo "   -noview: Set viewer to no view"
   echo ""
   goto END
BEND:
   exit 1
   
END:
   exit 0
