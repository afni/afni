#!/bin/tcsh -f

@global_parse `basename $0` "$*" ; if ($status) exit 0
set stat = 0
set stdir = $PWD

goto PARSE
RET_PARSE:

COPY_DATA:
      if ( ! -f $odir/amask+orig.HEAD ||   \
           ! -f $odir/T1+orig.HEAD ||  \
           ! -f $odir/PD+orig.HEAD   ) then
   3dcalc ${overW} -datum short -a $T1 -expr 'a' -prefix $odir/T1
   3dcalc ${overW} -datum short -a $PD -expr 'a' -prefix $odir/PD
   set afh = ($afh $odir/T1+orig.HEAD $odir/PD+orig.HEAD)
   if ($imask != '') then
      3dcalc ${overW} -datum byte -a $imask -expr 'a' -prefix $odir/umask
      set afh = ($afh umask+orig.HEAD)
   else
      3dmerge ${overW} -1blur_fwhm 25 -prefix $odir/PDsm $odir/PD+orig
      3dAutomask ${overW} -prefix $odir/amask $odir/PDsm+orig
      \rm -f $odir/PDsm+orig.* 
      set afh = ($afh amask+orig.HEAD)
   endif
      else
         echo "Reusing input data"
      endif
   
NEWMASK:
   cd $odir
   if ( "$menh" != "0.0" && ! -f hmask+orig.HEAD) then
      3dSkullStrip -head -input T1+orig -prefix SS
      3dcalc -a SS_head+orig -datum byte -expr 'step(a-1)' -prefix hmask
      set afh = ($afh hmask+orig.HEAD)
   else
      if ( "$menh" != "0.0" ) echo "Reusing hmask+orig.HEAD"
   endif
   cd $idir

STATIT:
   cd $odir
      if ( ! -f PD.ls+orig.HEAD ) then
   3dLocalstat -nbhd "SPHERE(25)" -stat perc:65:95:5 \
            -datum short -reduce_max_vox 5 \
            ${overW} -prefix PD.ls PD+orig.
         set afh = ($afh PD.ls+orig.HEAD)
      else
         echo "Reusing PD.ls"
      endif
   cd $idir
   
      
SCALEIT:
   cd $odir
   set med = `3dBrickStat -non-zero -median -mask amask+orig T1+orig.`
   if ("$menh" == 0.0) then
      if ( $imask != '') then
         set mm = umask+orig.HEAD
      else
         set mm = amask+orig.HEAD
      endif
      set meq = 'step(c)'
   else 
      set mm = hmask+orig.HEAD
      set meq = 'step(c)'
   endif
   if ($unmasked_output == 1) set meq = '1'
   
   if ( ! -f T1.uni+orig.HEAD) then
      3dcalc   -a T1+orig. -b PD.ls+orig'[perc:75.00]' \
                  -c $mm -datum short ${overW} \
                  -expr "$meq*a/b*$med[2]" \
                  -prefix T1.uni+orig.
      set afh = ($afh T1.uni+orig.HEAD)
   else
      echo "Reusing T1.uni+orig.HEAD"
   endif
   
   #One could also do the regular division by the PD (if there is a real
   #PD volume. The you can detect the bad apples by looking for those voxels 
   #who have markedly changed their ranking in the output compared to the input.
   #Bad apples would be zeroed out.
   
   cd $idir

HISTORY:
   cd $odir
   foreach ff ($afh)
      3dNotes -h "`basename $0` $argv" $ff
   end
   cd $idir
   
goto END

PARSE:
set Narg = $#
set cnt = 1
set starttime=`date`
set cleanafter = 1
set odir = ""
set olabel = 'loc_st'
set sigset = 'sigset'
set suff = 'nosuf'
set imask = ''
set idir = $PWD
set T1 = ''
set PD = ''
set menh = 0.0
set unmasked_output = 1
set afh = ()
set overW = ''

if ("$1" == '') goto HELP
while ($cnt <= $Narg)
   set donext = 1

   if ($donext && "$argv[$cnt]" == "-help" || "$argv[$cnt]" == "-h") then
      goto HELP
   endif
   
   if ($donext && ("$argv[$cnt]" == "-d" || "$argv[$cnt]" == "-echo")) then
      set echo
      set donext = 0   
   endif

   if ($donext && "$argv[$cnt]" == "-overwrite") then
      set overW = '-overwrite'
      set donext = 0   
   endif
   

   if ($donext && "$argv[$cnt]" == "-odir") then
      if ($cnt == $Narg) then
         echo "Need directory after -odir"
         goto END
      else
         @ cnt ++
         set odir = "$argv[$cnt]"
         set donext = 0   
      endif
   endif


   if ($donext && "$argv[$cnt]" == "-suffix") then
      if ($cnt == $Narg) then
         echo "Need a output suffix after -suffix"
         goto END
      else
         @ cnt ++
         set suff = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
   if ($donext && "$argv[$cnt]" == "-mask") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -mask"
         goto END
      else
         @ cnt ++
         set imask = "$argv[$cnt]"
         set donext = 0   
      endif
   endif


   if ($donext && ("$argv[$cnt]" == "-head_mask")) then
      set menh = 1
      set donext = 0   
   endif

   if ($donext && ("$argv[$cnt]" == "-unmasked_uni")) then
      set unmasked_output = 1
      set donext = 0   
   endif
   
   if ($donext && ("$argv[$cnt]" == "-masked_uni")) then
      set unmasked_output = 0
      set donext = 0   
   endif
   
   if ($donext && "$argv[$cnt]" == "-T1") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -T1"
         goto END
      else
         @ cnt ++
         set T1 = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
   if ($donext && "$argv[$cnt]" == "-PD") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -PD"
         goto END
      else
         @ cnt ++
         set PD = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
  if ($donext) then
      if (1) then
         echo "Parameter $argv[$cnt] not understood"
         apsearch -popt `basename $0` -word $argv[$cnt]
         goto END
      endif
   else 
      @ cnt ++
   endif 

end


#output directory
if ("$odir" == '') then
   set odir = "T1scale"
endif
if (! -d $odir) mkdir -p $odir
if (! -d $odir) then
   echo "Failed to create $odir"
   goto BEND
endif
cd $odir
set odir = $PWD
cd -
         
if ($T1 == '') then
   echo "No T1 volume"
   goto BEND
endif

if ($PD == '') then
   set PD = $T1
   echo "Setting PD to be same as T1"
endif



goto RET_PARSE

HELP:
   echo ""
   echo "Usage: `basename $0` <-T1 T1vol> <-PD PDvol> "
   echo " "
   echo "Fix bias field shading in T1 by scaling it with PD image."
   echo "You can also get a decent result even without the PD volume"
   echo ""
   echo "   -T1 T1vol: The T1 volume"
   echo "   -PD PDvol: The PD volume"
   echo "          (Psst, if you have no PD volume, put the T1 here too.)"
   echo "   -mask MVOL: Region to exclude"
   echo "         If not specified, will use 3dAutomask on fattened PDvol."
   echo "   -odir ODIR: Directory where output gets dumped. "
   echo "              Default is T1scale/ "
   echo "              ODIR will contain multiple volumes with the one"
   echo "              of most interest being T1.uni+orig" 
   echo ""
   echo "         Script will reuse existing  volumes"
   echo ""
   echo "   -head_mask: Mask output using 3dSkullStrip's -head option."
   echo "   -unmasked_uni: Do not apply masking to uniformized volume (default)"
   echo "   -masked_uni: Do apply masking to uniformized volume"
   echo "      NOTE: Masks are computed regardless of whether or not the mask "
   echo "            is applied to the uniformized image"
   echo ""
   echo "   -echo: Set echo"
   echo "   -help: this message" 
   echo ""
   @global_parse -gopts_help
   goto END

goto END

BEND:
set stat = 1
goto END

END:
cd $idir
exit $stat




