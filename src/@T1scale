#!/bin/tcsh -f

@global_parse `basename $0` "$*" ; if ($status) exit 0
set stat = 0
set stdir = $PWD

goto PARSE
RET_PARSE:

COPY_DATA:
      if ( ! -f $odir/amask+orig.HEAD ||   \
           ! -f $odir/T1+orig.HEAD ||  \
           ! -f $odir/PD+orig.HEAD   ) then
   3dcalc -overwrite -datum short -a $T1 -expr 'a' -prefix $odir/T1
   3dcalc -overwrite -datum short -a $PD -expr 'a' -prefix $odir/PD
   if ($imask != '') then
      3dcalc -overwrite -datum byte -a $imask -expr 'a' -prefix $odir/mask
   else
      3dmerge -1blur_fwhm 25 -prefix $odir/PDsm $odir/PD+orig
      3dAutomask -prefix $odir/amask $odir/PDsm+orig
      \rm -f $odir/PDsm+orig.* 
   endif
      else
         echo "Reusing input data"
      endif
   
STATIT:
   cd $odir
      if ( ! -f PD.ls+orig.HEAD ) then
   3dLocalstat -nbhd "SPHERE(25)" -stat perc:65:95:5 \
            -datum short -reduce_max_vox 5 \
            -prefix PD.ls PD+orig.
      else
         echo "Reusing PD.ls"
      endif
   cd $idir
   
NEWMASK:
   cd $odir
   if ( "$menh" != "0.0" && ! -f T1.hull.mask.m+orig.HEAD) then
      #Get enough voxels for 1 liter of goods
      set vv = `3dinfo -voxvol T1+orig`
      if ( ! -f T1.niml.hist ) then
         3dHist -input T1+orig -prefix T1
      else
         echo "Reusing T1.niml.hist"
      endif 
      set rcdfval = `ccalc "$menh*1e6/$vv"`
      set thout = (`3dHist -thishist T1.niml.hist -val_at rcdf $rcdfval`)
      set thout = $thout[2]    
      ConvexHull -isocmask "-a T1+orig -expr step(a-$thout)" \
                 -input T1+orig. -o_gii T1.hull
      3dSurfMask  -sv T1+orig. -fill_method fast -no_dist \
                  -i T1.hull.gii -prefix T1.hull.mask -overwrite
   else
      if ( "$menh" != "0.0" ) echo "Reusing T1.hull.mask.m+orig.HEAD+orig"
   endif
   cd $idir
      
SCALEIT:
   cd $odir
   set med = `3dBrickStat -non-zero -median -mask amask+orig T1+orig.`
   if ("$menh" == 0.0) then
      set mm = amask+orig.HEAD
      set meq = 'step(c)'
   else 
      set mm = T1.hull.mask.m+orig.HEAD
      set meq = 'step(c-1)'
   endif
   if ($unmasked_output == 1) set meq = '1'
   
   3dcalc   -a T1+orig. -b PD.ls+orig'[perc:75.00]' \
               -c $mm -datum short -overwrite \
               -expr "$meq*a/b*$med[2]" \
               -prefix T1.uni+orig.
   
   #One could also do the regular division by the PD (if there is a real
   #PD volume. The you can detect the bad apples by looking for those voxels 
   #who have markedly changed their ranking in the output compared to the input.
   #Bad apples would be zeroed out.
   
   cd $idir

HISTORY:
   cd $odir
   foreach ff (*.HEAD)
      3dNotes -h "`basename $0` $argv" $ff
   end
   cd $idir
   
goto END

PARSE:
set Narg = $#
set cnt = 1
set starttime=`date`
set cleanafter = 1
set odir = ""
set olabel = 'loc_st'
set sigset = 'sigset'
set suff = 'nosuf'
set imask = ''
set idir = $PWD
set T1 = ''
set PD = ''
set menh = 0.0
set unmasked_output = 0

if ("$1" == '') goto HELP
while ($cnt <= $Narg)
   set donext = 1

   if ($donext && "$argv[$cnt]" == "-help" || "$argv[$cnt]" == "-h") then
      goto HELP
   endif
   
   if ($donext && ("$argv[$cnt]" == "-d" || "$argv[$cnt]" == "-echo")) then
      set echo
      set donext = 0   
   endif


   if ($donext && "$argv[$cnt]" == "-odir") then
      if ($cnt == $Narg) then
         echo "Need directory after -odir"
         goto END
      else
         @ cnt ++
         set odir = "$argv[$cnt]"
         set donext = 0   
      endif
   endif


   if ($donext && "$argv[$cnt]" == "-suffix") then
      if ($cnt == $Narg) then
         echo "Need a output suffix after -suffix"
         goto END
      else
         @ cnt ++
         set suff = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
   if ($donext && "$argv[$cnt]" == "-mask") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -mask"
         goto END
      else
         @ cnt ++
         set imask = "$argv[$cnt]"
         set donext = 0   
      endif
   endif

   if ($donext && "$argv[$cnt]" == "-hullmask") then
      if ($cnt == $Narg) then
         echo "Need a value after -hullmask (1.0 is good)"
         goto END
      else
         @ cnt ++
         set menh = "$argv[$cnt]"
         set donext = 0   
      endif
   endif

   if ($donext && ("$argv[$cnt]" == "-unmasked_uni")) then
      set unmasked_output = 1
      set donext = 0   
   endif
   
   if ($donext && "$argv[$cnt]" == "-T1") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -T1"
         goto END
      else
         @ cnt ++
         set T1 = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
   if ($donext && "$argv[$cnt]" == "-PD") then
      if ($cnt == $Narg) then
         echo "Need a dataset suffix after -PD"
         goto END
      else
         @ cnt ++
         set PD = "$argv[$cnt]"
         set donext = 0   
      endif
   endif
   
  if ($donext) then
      if (1) then
         echo "Parameter $argv[$cnt] not understood"
         apsearch -popt `basename $0` -word $argv[$cnt]
         goto END
      endif
   else 
      @ cnt ++
   endif 

end


#output directory
if ("$odir" == '') then
   set odir = "T1scale"
endif
if (! -d $odir) mkdir -p $odir
if (! -d $odir) then
   echo "Failed to create $odir"
   goto BEND
endif
cd $odir
set odir = $PWD
cd -
         
if ($T1 == '') then
   echo "No T1 volume"
   goto BEND
endif

if ($PD == '') then
   set PD = $T1
   echo "Setting PD to be same as T1"
endif



goto RET_PARSE

HELP:
   echo ""
   echo "Usage: `basename $0` <-T1 T1vol> <-PD PDvol> "
   echo " "
   echo "Fix bias field shading in T1 by scaling it with PD image."
   echo "You can also get a decent result even without the PD volume"
   echo ""
   echo "   -T1 T1vol: The T1 volume"
   echo "   -PD PDvol: The PD volume"
   echo "          (Psst, if you have no PD volume, put the T1 here too.)"
   echo "   -mask MVOL: Region to exclude"
   echo "         If not specified, will use 3dAutomask on fattened PDvol."
   echo "   -odir ODIR: Directory where output gets dumped. "
   echo "              Default is T1scale/ "
   echo "              ODIR will contain multiple volumes with the one"
   echo "              of most interest being T1.uni+orig" 
   echo ""
   echo "         Script will reuse existing intermediate volumes"
   echo ""
   echo "   -hullmask VOL: Mask output using the convex hull of the brightest."
   echo "                  VOL liters worth of voxels. 1.0 is good start"
   echo "   -unmasked_uni: Do not apply masking to uniformized volume"
   echo "                  Masks are computed nonetheless"
   echo ""
   echo "   -echo: Set echo"
   echo "   -help: this message" 
   echo ""
   @global_parse -gopts_help
   goto END

goto END

BEND:
set stat = 1
goto END

END:
cd $idir
exit $stat




