#!/bin/tcsh

# destination for help output
set out_dir = help.out

# if the prog list comes from a build dir, set this
# default to current dir
if ( -f Makefile.INCLUDE ) then
   set src_dir = .
else
   set src_dir = ~/afni/src
endif


set progname = `basename $0`

# initialize bin_dir to the location of afni, or else the current directory
set bin_dir = `which afni`
if ( $status ) then
   set bin_dir = .
else
   set bin_dir = $bin_dir:h
endif

set ac = 1
while ( $ac <= $#argv )
  set oshort = `echo $argv[$ac] | cut -b 1-4`
  if ( "$argv[$ac]" == "-help" ) then
    echo ""
    echo "$progname           - run -help on AFNI programs"
    echo ""
    echo "      -help           : get this help"
    echo "      -bin_dir DIR    : specify directory of AFNI binaries"
    echo "      -src_dir DIR    : specify directory of AFNI source"
    echo ""
    echo "From the current or source directory, specified by -src_dir, this"
    echo "program will use Makefile.INCLUDE and 'make prog_list' to get a list"
    echo "of AFNI programs, and attempt to run -help on them all."
    echo ""
    echo 'It will run programs out of `which afni`, unless -bin_dir is given.'
    echo ""
    echo "R Reynolds   Nov 2018"
    echo ""

    exit
  else if ( "$oshort" == "-bin" ) then
    @ ac += 1
    set bin_dir = $argv[$ac]
  else if ( "$oshort" == "-src" ) then
    @ ac += 1
    set src_dir = $argv[$ac]
  else
    echo "** unknown opt: $argv[$ac]"
    exit 1
  endif
  
  @ ac += 1
end

echo "-- running programs out of binary directory $bin_dir"
echo "-- using source code from $src_dir"
echo "-- writing help output under $out_dir"
echo ""

# ======================================================================
# perform any useful checks
if ( ! -d $bin_dir ) then
   echo "** missing -bin_dir: $bin_dir"
   exit 1
endif

if ( ! -d $src_dir ) then
   echo "** missing -src_dir: $src_dir"
   exit 1
endif


# ======================================================================
# get a program list

# --------------------------------------------------
# option 1, get progs from src

if ( 1 ) then

   if ( ! -f $src_dir/Makefile.INCLUDE ) then
      echo "** missing: $src_dir/Makefile.INCLUDE"
      exit 1
   endif

   cd $src_dir
   make prog_list
   set progs = ( `grep -v '#' prog_list.txt` )
   cd -

else

   # --------------------------------------------------
   # option 2, get progs from help dir
   set progs = (`\ls ~/.afni/help | grep '\.complete$' | sed 's/\.complete//'`)

endif


# ======================================================================
# make a working directory
if ( -d $out_dir ) \rm -fr $out_dir
mkdir $out_dir


# ======================================================================
# get to work

set good_stats = ()
set bad_stats = ()
foreach prog ( $progs )

  printf "%-30s " $prog
  $bin_dir/$prog -help >& $out_dir/$prog.help

  if ( $status ) then
     echo "                 BAD_STAT"
     set bad_stats = ( $bad_stats $prog )
  else
     echo GOOD
     set good_stats = ( $good_stats $prog )
  endif
end

echo ""
echo "found $#good_stats good progs and $#bad_stats issues out of $#progs"
echo ""
echo bad stats for: $bad_stats


##### consider writing $good_stats to a file, or even with $bad_stats
