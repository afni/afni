#!/bin/tcsh -fe

set stat = 0
set sdir = $PWD


goto PARSE
RET_PARSE:

if ( -d ./3dPFM_Demo_sample) then
echo ""
echo "ERROR:"
echo "   Directory ./3dPFM_Demo_sample exists already"
echo "   If you want to recreate it, remove it with:"
echo "\rm -rf ./3dPFM_Demo_sample"
echo "   then run `basename $0` $argv[*] again."
echo ""
echo "Otherwise to run demos, see content of ./3dPFM_Demo_sample/README.txt"
echo ""
goto END
   endif

which curl
if ($status) then
   wget http://afni.nimh.nih.gov/pub/dist/tgz/3dPFM_Demo_sample.tgz
else
   curl -O http://afni.nimh.nih.gov/pub/dist/tgz/3dPFM_Demo_sample.tgz
endif
tar xvzf 3dPFM_Demo_sample.tgz
cd 3dPFM_Demo_sample
   echo "********************************************************"
   echo "Follow examples in ./3dPFM_Demo_sample/README.txt "
   echo "********************************************************"
cd -
goto END

PARSE:
   set Narg = $#
   set cnt = 1
   while ($cnt <= $Narg)
		set donext = 1;
      if ($donext && "$argv[$cnt]" == "-echo") then
         set echo
         set donext = 0; goto NEXT		
      endif
      if ($donext && ("$argv[$cnt]" == "-h" || "$argv[$cnt]" == "-help")) then
         goto HELP
         set donext = 0;	 goto NEXT	
      endif
      if ($donext == 1) then
         echo "Error: Option or parameter '$argv[$cnt]' not understood"
         goto END
      endif
      
      NEXT:
		@ cnt ++
	end
   
goto RET_PARSE

HELP:
   echo "Installs the demo archive for the 3dPFM function"
   echo "After the archive is downloaded and unpacked, see its README.txt"
   echo "for details."
   goto END

BEND:
   echo "Failed"
   set stat = 1
   goto END
   
END:
   exit $stat
