#File Generated by Hans J. Johnson
#Please contact hans-johnson@uiowa.edu for making enhancments/corrections
PROJECT(FSLIOLIB)

# Apply the common nifti library version by default. To change this behaviour
# simply define FSLIOLIB_SHAREDLIB_VERSION before this line
IF (NOT DEFINED FSLIOLIB_SHAREDLIB_VERSION)
  SET (FSLIOLIB_SHAREDLIB_VERSION ${NIFTI_SHAREDLIB_VERSION})
ENDIF (NOT DEFINED FSLIOLIB_SHAREDLIB_VERSION)

# extract FSLIOLIB soname from the version string
STRING( REGEX MATCH "^[0-9]+" FSLIOLIB_SHAREDLIB_SOVERSION ${FSLIOLIB_SHAREDLIB_VERSION})

INCLUDE_DIRECTORIES(${NIFTILIB_SOURCE_DIR} ${ZNZLIB_SOURCE_DIR})
SET(FSLIOLIB_SRC fslio.c)

# Michael Hanke 2004-04-25:
# Restructure the file to match those of niftilib and znzlib
SET(NIFTI_FSLIOLIB_NAME ${PACKAGE_PREFIX}fslio)

ADD_LIBRARY(${NIFTI_FSLIOLIB_NAME} ${FSLIOLIB_SRC} )
TARGET_LINK_LIBRARIES( ${NIFTI_FSLIOLIB_NAME} ${PACKAGE_PREFIX}niftiio)

# Set lib version when buildung shared libs.
IF (BUILD_SHARED_LIBS)
  SET_TARGET_PROPERTIES(${NIFTI_FSLIOLIB_NAME} PROPERTIES SOVERSION ${FSLIOLIB_SHAREDLIB_SOVERSION} VERSION ${FSLIOLIB_SHAREDLIB_VERSION})
ENDIF (BUILD_SHARED_LIBS)

IF(NOT NIFTI_INSTALL_NO_LIBRARIES)
  INSTALL(TARGETS ${NIFTI_FSLIOLIB_NAME}
    RUNTIME DESTINATION ${NIFTI_INSTALL_BIN_DIR} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${NIFTI_INSTALL_LIB_DIR} COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_LIBRARIES)

IF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
  FILE(GLOB __files "${CMAKE_CURRENT_SOURCE_DIR}/*.h")
  INSTALL(FILES ${__files}
    DESTINATION ${NIFTI_INSTALL_INCLUDE_DIR}
    COMPONENT Development)
ENDIF(NOT NIFTI_INSTALL_NO_DEVELOPMENT)
