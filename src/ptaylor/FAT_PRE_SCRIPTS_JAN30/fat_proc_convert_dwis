#!/bin/tcsh -ef



# --------------------- revision history -------------------------
#
# Jan, 2017
#   + rename
#
# Jan 27, 2017
#   + update opts
#   + use 1dDW_Grad_o_Mat++

# ----------------------------------------------------------------

set version = "2.2"
set rev_dat = "Jan, 2017"
set this_prog = "fat_proc_convert_dwis"

set here     = $PWD
set idir_ap  = ""
set idir_pa  = ""
set odir_ap  = ""
set odir_pa  = ""
set o_ap     = "AP"        # default file output prefix
set o_pa     = "PA"        # default file output prefix
set ori_new  = "RPI"       # default file output orientation
set doflip   = ""          # default: no flip of grads
set DO_REORI = 1

# ------------------- process options, a la rr ----------------------

if ( $#argv == 0 ) goto SHOW_HELP

set ac = 1
while ( $ac <= $#argv )
    # terminal options
    if ( ("$argv[$ac]" == "-h" ) || ("$argv[$ac]" == "-help" )) then
        goto SHOW_HELP
    endif
    if ( "$argv[$ac]" == "-ver" ) then
        goto SHOW_VERSION
    endif

    # ------------- input(s) ----------------
    if ( "$argv[$ac]" == "-indir_ap" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set idir_ap = "$argv[$ac]"

    else if ( "$argv[$ac]" == "-indir_pa" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set idir_pa = "$argv[$ac]"
        
    # ------------- output(s) ----------------
    else if ( "$argv[$ac]" == "-outdir_ap" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set odir_ap = "$argv[$ac]"
        
    else if ( "$argv[$ac]" == "-outdir_pa" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set odir_pa = "$argv[$ac]"
        
    else if ( "$argv[$ac]" == "-prefix_ap" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set o_ap = "$argv[$ac]"

    else if ( "$argv[$ac]" == "-prefix_pa" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set o_pa = "$argv[$ac]"

    # -------------- other opts ----------------
    else if ( "$argv[$ac]" == "-orient" ) then
        if ( $ac >= $#argv ) goto FAIL_MISSING_ARG
        @ ac += 1
        set ori_new = "$argv[$ac]"
        
    else if ( "$argv[$ac]" == "-flip_x" ) then
        set doflip = "-flip_x"

    else if ( "$argv[$ac]" == "-flip_y" ) then
        set doflip = "-flip_y"

    else if ( "$argv[$ac]" == "-flip_z" ) then
        set doflip = "-flip_z"

    else if ( "$argv[$ac]" == "-no_flip" ) then
        set doflip = "-no_flip"

    # -------------- more other opts ----------------
    else if ( "$argv[$ac]" == "-reorig_reorient_off" ) then
        set DO_REORI = 0          # leave 'em alone!

    else
        echo "** unexpected option #$ac = '$argv[$ac]'"
        goto BAD_EXIT

    endif
    @ ac += 1
end


# =======================================================================
# ============================ ** SETUP ** ==============================
# =======================================================================

echo "++ Start script version: $version"

# ============================= dicom dir ===============================

if ( ("$idir_ap" == "") && ("$idir_pa" == "") ) then
    echo "** ERROR: need to input at least one DICOM directory,"
    echo "\t using either after '-indir_ap' or '-indir_pa'."
    goto BAD_EXIT
endif

if ( "$idir_ap" != "" ) then
    set idir_ap = `ls -d $idir_ap`
    set Nap = $#idir_ap
    echo "++ Found $Nap directories from '-indir_ap $idir_ap':"
    echo "\t $idir_ap"
endif

if ( "$idir_pa" != "" ) then
    set idir_pa = `ls -d $idir_pa`
    set Npa = $#idir_pa
    echo "++ Found $Npa directories from '-indir_pa $idir_pa':"
    echo "\t $idir_pa"
endif

# ============================= input dir ===============================

# check for old relics

foreach dd ( $idir_ap $idir_pa ) 

    cd $here
    cd $dd

    echo "++ Checking ${dd} for preexisting NIFTIs ... "

    if ( (`find . -maxdepth 1 -type f -name "*nii" | wc -l` > 0 ) || \
         (`find . -maxdepth 1 -type f -name "*nii.gz" | wc -l` > 0 ) ) then
        echo "\n** ERROR: already some *.nii.gz files in $dd !"
        goto BAD_EXIT
    endif
end

cd $here
echo "++ --> OK, good, no competing NIFTIs in any DICOM directory."

# ============================= output dirs =============================

# check output directory, use default if nec

# default output dir, if nothing input.
if ( "$idir_ap" != "" ) then

    if ("$odir_ap" == "") then
        set odir_ap = "$idir_ap[1]/../UNFILT_AP"
        echo "\n++ No output 'AP' directory specified by the user."
        echo "\t Using default location/naming output directory:"
        echo "\t   $odir_ap"
    endif

    if ( -e "$odir_ap" ) then
        echo "\n+* WARNING: already some output directory $odir_ap !"
    else
        mkdir $odir_ap
    endif
endif

if ( "$idir_pa" != "" ) then

    if ("$odir_pa" == "") then
        set odir_pa = "$idir_pa[1]/../UNFILT_PA"
        echo "\n++ No output 'PA' directory specified by the user."
        echo "\t Using default location/naming output directory:"
        echo "\t   $odir_pa"
    endif

    if ( -e "$odir_pa" ) then
        echo "\n+* WARNING: already some output directory $odir_ap !"
    else
        mkdir $odir_pa
    endif
endif

# =======================================================================
# =========================== ** PROCESS ** =============================
# =======================================================================

# ======================== convert dicoms ===============================

if ( "$idir_ap" != "") then
    foreach dd ( $idir_ap )
        echo "\n++ OK, now converting dicoms in: $dd"
        dcm2nii -o "$odir_ap" $dd/*
    end
endif

if ( "$idir_pa" != "") then
    foreach dd ( $idir_pa )
        echo "\n++ OK, now converting dicoms in: $dd"
        dcm2nii -o "$odir_pa" $dd/*
    end
endif

# ============ do the major rearranging/sorting/combining ================

# ----> future note: simplify scripting with loop instead of lame
# ----> copying...

# Use this to give AP and PA same origin....
set have_orig = 0  
set ORIG = ( 0 0 0 )

# first for AP, if they are input
if ( "$odir_ap" != "") then

    cd $here
    cd $odir_ap

    set allfile = `ls *`
    
    # push all into OTHER/, and then bring back the useful sets.
    mkdir OTHER
    foreach ff ( $allfile )
        \mv $ff OTHER/.
    end
    cd OTHER

    set all_bvec = `ls *bvec`
    @ Nbvec = $#all_bvec
    echo "++ Found $Nbvec sets of DTI data in ${odir_ap}..."
    echo "   Organizizing now."

    # overkill, but make sure everything stays sorted in proper order
    set list_cvecs = ( )
    set list_vols  = ( )

    foreach i ( `seq 1 1 $Nbvec` )
        @ ii = $i - 1
        set ff = "$all_bvec[$i]"
        set gg = ${ff:gas/.bvec//}

        # combine bvals and bvec, NEWER func call
        1dDW_Grad_o_Mat++                          \
            -in_row_vec   ${gg}.bvec               \
            -in_bvals     ${gg}.bval               \
            $doflip                                \
            -out_col_vec _TEMP_${ii}.cvec

        if ( $DO_REORI ) then
            3dresample                             \
                -overwrite                         \
                -orient $ori_new                   \
                -prefix _TEMP_${ii}.nii.gz         \
                -inset  ${gg}.nii.gz
        else
            # chicken...
            3dcalc                                 \
                -a ${gg}.nii.gz                    \
                -expr 'a'                          \
                -prefix _TEMP_${ii}.nii.gz         \
                -overwrite
        endif

        set list_cvecs = ( $list_cvecs _TEMP_${ii}.cvec )
        set list_vols  = ( $list_vols  _TEMP_${ii}.nii.gz )
    end

    # Combine everything
    set all_cvecs = "_TEMP_ALL.cvec"
    set fin_bvecs = "../${o_ap}.bvec"
    set fin_bvals = "../${o_ap}.bval"
    set fin_vols  = "../${o_ap}.nii"

    # clear away a file, if *somehow* there? (shouldn't be...)
    if ( -e $all_cvecs ) then
        \rm $all_cvecs
    endif
    foreach ff ( $list_cvecs )
        cat $ff >> $all_cvecs
    end
    3dTcat                             \
        -overwrite                     \
        -prefix $fin_vols              \
        $list_vols

    if ( $DO_REORI ) then
        # set the (0,0,0) to the CM of the brain, or apply the previously
        # calc'ed origin. (Aug., 2016)
        if ( $have_orig ) then
            echo "++ Apply known appropriate origin."
            3drefit                        \
                -xorigin_raw $ORIG[1]      \
                -yorigin_raw $ORIG[2]      \
                -zorigin_raw $ORIG[3]      \
                $fin_vols
        else
            echo "++ Set center of brain to center of mass..."
            3dCM -automask -set 0 0 0 $fin_vols
            echo "++ ... and remember it."
            set ORIG = `3dinfo -o3 $fin_vols`
            set have_orig = 1
        endif
    endif

    # make bval/bvec/*.nii files
    1dDW_Grad_o_Mat++                          \
        -in_col_vec       $all_cvecs           \
        -out_row_bval_sep $fin_bvals           \
        -unit_mag_out                          \
        -out_row_vec      $fin_bvecs
endif 


# then for PA, if they are input
if ( "$odir_pa" != "") then

    cd $here
    cd $odir_pa

    set allfile = `ls *`
    
    # push all into OTHER/, and then bring back the useful sets.
    mkdir OTHER
    foreach ff ( $allfile )
        \mv $ff OTHER/.
    end
    cd OTHER

    set all_bvec = `ls *bvec`
    @ Nbvec = $#all_bvec
    echo "++ Found $Nbvec sets of DTI data in ${odir_pa}..."
    echo "   Organizizing now."

    # overkill, but make sure everything stays sorted in proper order
    set list_cvecs = ( )
    set list_vols  = ( )

    foreach i ( `seq 1 1 $Nbvec` )
        @ ii = $i - 1
        set ff = "$all_bvec[$i]"
        set gg = ${ff:gas/.bvec//}

        # combine bvals and bvec, NEWER func call
        1dDW_Grad_o_Mat++                          \
            -in_row_vec   ${gg}.bvec               \
            -in_bvals     ${gg}.bval               \
            $doflip                                \
            -out_col_vec _TEMP_${ii}.cvec

        if ( $DO_REORI ) then
            3dresample                             \
                -overwrite                         \
                -orient $ori_new                   \
                -prefix _TEMP_${ii}.nii.gz         \
                -inset  ${gg}.nii.gz
        else
            # chicken...
            3dcalc                                 \
                -a ${gg}.nii.gz                    \
                -expr 'a'                          \
                -prefix _TEMP_${ii}.nii.gz         \
                -overwrite
        endif

        set list_cvecs = ( $list_cvecs _TEMP_${ii}.cvec )
        set list_vols  = ( $list_vols  _TEMP_${ii}.nii.gz )
    end

    # Combine everything
    set all_cvecs = "_TEMP_ALL.cvec"
    set fin_bvecs = "../${o_pa}.bvec"
    set fin_bvals = "../${o_pa}.bval"
    set fin_vols  = "../${o_pa}.nii"

    # clear away a file, if *somehow* there? (shouldn't be...)
    if ( -e $all_cvecs ) then
        \rm $all_cvecs
    endif
    foreach ff ( $list_cvecs )
        cat $ff >> $all_cvecs
    end
    3dTcat                             \
        -overwrite                     \
        -prefix $fin_vols              \
        $list_vols

    if ( $DO_REORI ) then
        # set the (0,0,0) to the CM of the brain, or apply the previously
        # calc'ed origin. (Aug., 2016)
        if ( $have_orig ) then
            echo "++ Apply known appropriate origin."
            3drefit                        \
                -xorigin_raw $ORIG[1]      \
                -yorigin_raw $ORIG[2]      \
                -zorigin_raw $ORIG[3]      \
                $fin_vols
        else
            echo "++ Set center of brain to center of mass..."
            3dCM -automask -set 0 0 0 $fin_vols
            echo "++ ... and remember it."
            set ORIG = `3dinfo -o3 $fin_vols`
            set have_orig = 1
        endif
    endif

    # make bval/bvec/*.nii files
    1dDW_Grad_o_Mat++                          \
        -in_col_vec       $all_cvecs           \
        -out_row_bval_sep $fin_bvals           \
        -unit_mag_out                          \
        -out_row_vec      $fin_bvecs
endif 


# ================ display outputs  ====================

echo "\n++ All done!\n"

if ( "$odir_ap" != "") then

    cd $here
    cd $odir_ap

    set all_out = `ls *{bval,bvec,nii}`
    echo "++ In ${PWD}:"
    if ( $#all_out >= 1 ) then
        foreach ff ( $all_out )
            echo "\t $ff"
        end
    else
        echo "** ERROR: No files, even though some should be here??"
    endif
endif

if ( "$odir_pa" != "") then

    cd $here
    cd $odir_pa

    set all_out = `ls *{bval,bvec,nii}`
    echo "++ In ${PWD}:"
    if ( $#all_out >= 1 ) then
        foreach ff ( $all_out )
            echo "\t $ff"
        end
    else
        echo "** ERROR: No files, even though some should be here??"
    endif
endif

goto GOOD_EXIT

# ========================================================================
# ========================================================================

SHOW_HELP:
cat << EOF
-------------------------------------------------------------------------

    The purpose of this function is to help convert one or more sets
    of DWIs in DICOM format into 'nicer' volume+grad format.  This 
    function can process AP/PA (AKA 'blip up'/'blip down') data sets in 
    parallel.  Also, if multiple directories of DICOMS exist for a given
    AP or PA set, then those can be combined into a single volume+grad 
    file.  Data sets are also converted to have a single orientation.

    This program can be used to: convert dicoms to NIFTI (with bvals
    and bvecs); reorient volumetric data; and glue together multiple
    sessions/directories of data (may the user choose what is
    appropriate to combine!).  More functionality could be demanded by
    demanding users.

    REQUIRES: AFNI, dcm2nii.

    Ver. $version (PA Taylor, ${rev_dat})

-------------------------------------------------------------------------

  RUNNING: at least one of the two '-indir_* ...' directories needs to
           be given; both can be, and the processing happens in
           parallel, producing two output directories. The 'AP' vs
           'PA' designation is not very important-- it is really a
           naming convention to differentiate the separate phase
           encodings.

  \$ $this_prog  \
      -indir_ap   DIR_AP                       \
      -indir_pa   DIR_PA                       \
      {-outdir_ap OUT_AP}                      \
      {-outdir_pa OUT_PA}                      \
      {-prefix_ap PRE_AP}                      \
      {-prefix_pa PRE_PA}                      \
      {-orient    ORIENT}                      \
      {-flip_x | -flip_y | -flip_z | -no_flip}

  where:
  -indir_ap  DIR_AP :directory name(s) of (only) DICOM files of the DWI
                      data,designated as having 'AP' phase encoding.
  -indir_pa  DIR_PA :directory name(s) of (only) DICOM files of the DWI
                      data, designated as having 'PA' phase encoding.
         ----> these inputs are processed in parallel; DIR_AP can actually
               contain a wildcard expression for several directories, if,
               for example, multiple DWI sets should be glued together into
               one set (and the same is analogously true for DIR_PA). 
               NOTE: if specifying more than one directory, put double
               quotes around your entry, like: "file*".


  -outdir_ap OUT_AP :single output directory name for the 'AP' set(s);
                     default is 'UNFILT_AP', placed parallel to DIR_AP.
  -outdir_pa OUT_PA :single output directory name for the 'PA' set(s);
                     default is 'UNFILT_PA', placed parallel to DIR_PA.
  -prefix_ap PRE_AP :output prefix for the *.nii, *.bvec and *.bval files
                     in OUT_AP (default is '$o_ap').
  -prefix_pa PRE_PA :output prefix for the *.nii, *.bvec and *.bval files
                     in OUT_PA (default is currently '$o_pa').

  -orient    ORIENT :optional chance to reset orientation of the volume
                     files (default is currently '$ori_new').
  -reorig_reorient_off: switch to turn of the nicety of putting (0, 0, 0)
                     at brain's center of mass (-> 'reorigin' calc) and to
                     not reorient data (-> 'reorient' calc).  Could lead
                     to weirdness later on down the road, depending on the
                     data and headers (ergo, not recommended.)

  -flip_x | -flip_y | -flip_z | -no_flip
                    :use any one of these for the ability to flip grads 
                     while processing with 1dDW_Grad_o_Mat++. (Default is 
                     to not flip.)

-------------------------------------------------------------------------

  OUTPUTS:

    For a given phase encoding set (and both can be processed
    simultaneously, in parallel), the output directory contains a
    NIFTI file with N volumes, a row-wise (3xN) bvec file of the
    gradient orientations, and a row-wise (1xN) bval file of the
    gradient magnitudes; these files are respectively named: 
        PREFIX.nii 
        PREFIX.bvec 
        PREFIX.bval
    and they are meant to mimic the trio of files output by dcm2nii.

-------------------------------------------------------------------------

  EXAMPLE:
    $this_prog  \
        -indir_ap  "SUB001/DTI_AP_0*"           \
        -indir_pa  "SUB001/DTI_PA_0*"           \
        -outdir_ap  SUB001/UNFILT_AP            \
        -outdir_pa  SUB001/UNFILT_PA  

    or

    $this_prog  \
        -indir_ap  "DTI_DICOMS"                 \
        -outdir_ap  UNFILT_DTI

-------------------------------------------------------------------------

EOF

    goto GOOD_EXIT

SHOW_VERSION:
    echo "version  $version (${rev_dat})"
    goto GOOD_EXIT

BAD_EXIT:
    exit 1

# send everyone here, in case there is any cleanup to do
GOOD_EXIT:
    exit 0
