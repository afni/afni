#!/bin/tcsh

# Script to generate nodes on standard surface meshes for known APQC
# HTML resting state seed-based correlation.
#
# Should be able to get similar nodes on any standard surface; using
# MNI here.
#
# auth = PA Taylor (SSCC, NIMH, NIH, USA)
#
#set ver  = "1.0"; set date = "May 11, 2022"
# + [PT] creation
#
set ver  = "1.1"; set date = "May 11, 2022"
# + [PT] realizing 'space' is unnecessary with standard mesh, but we
#   should have a surface type label: software that created it, and
#   species/age/etc.
#
# ==========================================================================

### where to run this script (or wherever suma_MNI152_2009.tgz has
### been downloaded+unpacked:
# cd ~/.afni/data/suma_MNI152_2009

# the main output, to be distributed in AFNI
set otxt     = afni_nodes_per_space.txt   

# get seed locations in RAI coords in MNI space from AFNI-distributed file
set wafni     = `which afni`
set adir      = `dirname ${wafni}`
set file_seed = ${adir}/afni_seeds_per_space.txt   # The File

set surfvol   = MNI152_2009_SurfVol.nii
set surflab   = "FS_human_adult"

# -------------------------------------------------------------------------

# similar to ${file_seed} file
cat  <<EOF > ${otxt}
# List of node locations for APQC (and possibly other applications).
# Nodes are defined for standard FreeSurfer meshes (and maybe some other 
# standard meshes, too, over time.  These were derived from 
# suma_MNI152_2009.tgz (downloadable from AFNI website) and the
# pre-existing, volumetrically defined seed locations in
# afni_seeds_per_space.txt (distributed in AFNI repo).
#
# This file was generated by running: do_make_apqc_nodes.tcsh
#
# -----------------------------------------------------------------------------
# ver  = ${ver}  ;  date = ${date}
# -----------------------------------------------------------------------------
#     node     hemi     ldv     surf_label           roi_label            netw
# -----------------------------------------------------------------------------
EOF



# loop over all networks
set all_netw = ( DMN Vis Aud )
set all_ldv  = ( 60 141 )

set file_node = nodelist_xyz.1D

printf "" > ${file_node}

foreach netw ( ${all_netw} )
    echo "++ NETWORK:  ${netw}"
    set line = `grep MNI ${file_seed} | grep --color=never ${netw}`

    printf "%8.4f  %8.4f  %8.4f\n" ${line[1]} ${line[2]} ${line[3]} \
        > ${file_node}

    foreach ldv ( ${all_ldv} )
        echo "++ LDV:  ${ldv}"
        set node_info = `Surf2VolCoord                               \
                            -RAI                                     \
                            -i std.${ldv}.lh.pial.gii                \
                            -i std.${ldv}.rh.pial.gii                \
                            -sv ${surfvol}                           \
                            -qual LR                                 \
                            -closest_nodes ${file_node}`

        echo "   $node_info"
        set hemi = `echo ${node_info[1]} \
                        | awk '{print substr($0,length($0),1)}'`

        if ( "${hemi}" == "R" ) then 
            set hh = "rh"
        else if ( "${hemi}" == "L" ) then 
            set hh = "lh"
        else
            echo "** ERROR: unrecognized hemisphere here, hemi is '${hemi}'"
            exit 1
        endif
                            
        printf "%10s     %-4s  %6s     %-12s       %-16s     %-5s\n" \
            "${node_info[1]}" "${hh}" "${ldv}" "${surflab}" \
            "${line[5]}" "${line[6]}" \
            >> ${otxt}

    end
end

# ---------------------------------------------------------------------------
# done; display

cat <<EOF

The file contents:
EOF

echo "-------------------------------------------------------------------------"
cat ${otxt}
echo "-------------------------------------------------------------------------"

exit 0
